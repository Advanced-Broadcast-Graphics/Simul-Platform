// Copyright (c) 2015 Simul Software Ltd. All rights reserved.
#include "shader_platform.sl"
#include "../SL/common.sl"
#include "../SL/render_states.sl"
#include "../SL/solid_constants.sl"
#include "../SL/depth.sl"

uniform Texture2D diffuseTexture;
uniform TextureCube diffuseCubemap;

//////////////////////////
uniform TextureCube nearFarTextureCube;
uniform Texture2D loss2dTexture;
uniform Texture2D shadowTexture;
uniform Texture3D inscatterVolumeTexture;
//////////////////////////

struct vertexInput
{
    vec3 position		: POSITION;
    vec2 texCoords		: TEXCOORD0;
    vec3 normal			: TEXCOORD1;
};

struct vertexOutput
{
    vec4 hPosition		: SV_POSITION;
    vec2 texCoords		: TEXCOORD0;
    vec3 normal			: TEXCOORD1;
    vec3 view			: TEXCOORD3;
    vec4 clip_pos		: TEXCOORD4;
};

shader vertexOutput VS_Solid(vertexInput IN)
{
    vertexOutput OUT;
	vec4 opos		=vec4(IN.position.xyz,1.0);
    vec3 wpos		=mul(world, opos).xyz;
	OUT.view		=normalize(wpos-viewPosition);
    OUT.clip_pos	=mul(worldViewProj, opos);
	OUT.texCoords	=IN.texCoords;
#ifdef GLSL
    OUT.normal.xyz	=mul(mat3(world),IN.normal);
#else
    OUT.normal.xyz	=mul(world,vec4(IN.normal,0.0)).xyz;
#endif
	OUT.hPosition	=OUT.clip_pos;
    return OUT;
}

shader vec4 PS_Solid(vertexOutput IN) : SV_TARGET
{
    vec3 normal		=normalize(IN.normal);
    vec4 c			=texture_wrap(diffuseTexture,vec2(IN.texCoords.x,1.0-IN.texCoords.y));
	vec3 totalDiff	=sample_3d(diffuseCubemap,cubeSamplerState,-normal.xyz).rgb;
	c.a				=1.0;
	totalDiff		+=0.3*saturate(dot(lightDir,normal.xyz))*lightIrradiance;
	c.rgb			*=totalDiff;
    vec4 u			=vec4(c.rgb,1.0);
	u.rgb			=vec3(0,1,0);
	return u;
}

/// A function that applies atmospherics and cloud visibility to a pixel of a transparent object.
void AtmosphericsAndCloudOcclusion(out vec4 loss,out vec3 add
				,vec4 clip_pos
				,vec3 view
				,TextureCube nearFarTextureCube
				,Texture2D loss2dTexture
				,Texture2D shadowTexture
				,Texture3D inscatterVolumeTexture
				,vec4 depthToLinFadeDistParams)
{
	clip_pos/=clip_pos.w;
	// we only care about view.z, i.e. the third element of the vector.
	// so only dot-product the third row of invViewProj, with clip_pos.
	float sine					=normalize(view).z;
	vec2 texCoords				=0.5*vec2(clip_pos.x+1.0,1.0-clip_pos.y);
	//vec2 lowResTexCoords		=fullResToLowResTransformXYWH.xy+texCoords*fullResToLowResTransformXYWH.zw;

	vec4 nearFarCloud			=texture_wrap_lod(nearFarTextureCube	,view		,0);

	// translate from depth to linear distance.
	DepthInterpretationStruct depthInterpretationStruct;
	depthInterpretationStruct.depthToLinFadeDistParams=depthToLinFadeDistParams;
	depthInterpretationStruct.reverseDepth=(reverseDepth!=0);
	float dist					=depthToLinearDistance(clip_pos.z	,depthInterpretationStruct);
	float dist_rt				=pow(dist,0.5);
	const float pi				=3.1415926536;
	vec3 worldspaceVolumeTexCoords	=vec3(atan2(view.x,view.y)/(2.0*pi),0.5*(1.0+2.0*asin(sine)/pi),dist_rt);
	
	// Inscatter from air:
	vec4 insc						=texture_3d_wmc_lod(inscatterVolumeTexture,worldspaceVolumeTexCoords,0);
	
	vec2 loss_texc				=vec2(dist_rt,0.5*(1.f-sine));

	// Visibility due to clouds:
	float visibility			=1.0-saturate((dist-nearFarCloud.w) / (0.001));//(nearFarCloud.x-nearFarCloud.y));

	insc.rgb					*=visibility;
	// loss due to air (alpha is visilibity due to cloud occlusion).
	loss						=vec4(texture_clamp_lod(loss2dTexture,loss_texc,0).rgb,visibility);
	add							=insc.rgb;
}

/// An example shader for rendering transparent objects.
shader vec4 PS_Transparent(vertexOutput IN) : SV_TARGET
{
    vec3 normal	=normalize(IN.normal);
// Here is whatever colour your shader would normally generate in the absence of atmospherics:
    vec4 c = vec4(0.5,0.5,0.5,0.5);

// Now we calculate the atmospheric effects:
	vec4 loss;
	vec3 add;
// We need clip_pos and view direction (i.e. the direction in world xyz to the pixel from the camera).
	AtmosphericsAndCloudOcclusion(loss,add
				,IN.clip_pos
				,IN.view
				,nearFarTextureCube
				,loss2dTexture
				,shadowTexture
				,inscatterVolumeTexture
				,depthToLinFadeDistParams
				);
// Loss is multiplied over the original colour, and inscatter is added:
	c.rgba*=loss.rgba;
	c.rgb+=add;
	return c;
}

VertexShader vs_solid = CompileShader(vs_4_0, VS_Solid());
PixelShader ps_solid = CompileShader(ps_4_0, PS_Solid());
technique solid
{
    pass base 
    {
		SetRasterizerState(RenderNoCull);
//SetDepthStencilState(EnableDepth,0);
		SetBlendState(DontBlend,float4(0.0f, 0.0f, 0.0f, 0.0f), 0xFFFFFFFF );
		SetVertexShader(vs_solid);
        SetGeometryShader(NULL);
		SetPixelShader(ps_solid);
    }
}


technique transparent
{
    pass base 
    {
		SetRasterizerState(RenderBackfaceCull);
		//SetBlendState(DontBlend,float4(0.0f, 0.0f, 0.0f, 0.0f), 0xFFFFFFFF );
		SetBlendState(AlphaBlend,float4(0.0f, 0.0f, 0.0f, 0.0f), 0xFFFFFFFF );
		SetVertexShader(CompileShader(vs_4_0,VS_Solid()));
        SetGeometryShader(NULL);
		SetPixelShader(CompileShader(ps_4_0,PS_Transparent()));
    }
}
