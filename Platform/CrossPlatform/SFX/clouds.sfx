//  Copyright (c) 2015 Simul Software Ltd. All rights reserved.
#include "shader_platform.sl"
#include "../SL/common.sl"
#include "../SL/render_states.sl"
#include "../SL/simul_inscatter_fns.sl"
#include "../SL/simul_cloud_constants.sl"
#include "../SL/depth.sl"
uniform Texture3D smallWorleyTexture3D				SIMUL_TEXTURE_REGISTER(12);
#include "../SL/simul_clouds.sl"
#include "../SL/clouds_simple.sl"
#include "../SL/states.sl"
#include "../SL/earth_shadow_fade.sl"
#include "../SL/debug_constants.sl"

uniform Texture3D cloudDensity	;
uniform Texture3D cloudDensity2	;

uniform Texture2D cloudShadowTexture;
uniform Texture2D lossTexture;
uniform Texture2D inscTexture;
uniform Texture2D skylTexture;
uniform TextureCube depthTexture;
uniform Texture3D noiseTexture3D;
uniform TextureCube clouds2DTexture	;
uniform Texture2D illuminationTexture;
uniform Texture2D lightTableTexture	;
uniform Texture2D rainbowLookupTexture	;
uniform Texture2D coronaLookupTexture	;
uniform Texture3D volumeInscatterTexture;
uniform Texture2D rainMapTexture			;
/// The previous texture area:
uniform Texture2D updateTexture				;
uniform RWTexture3D<uchar4> inputTexture3d;
uniform RWTexture3D<uchar4> outputTexture3d;
uniform RWStructuredBuffer<VolumeQueryResult> volumeQueries;

uniform StructuredBuffer<vec3> queryInputs;

uniform RWStructuredBuffer<LineQueryResult> lineQueryResults;

uniform RWTexture2D<vec4> outputTexture1		SIMUL_RWTEXTURE_REGISTER(0);
uniform RWTexture2D<vec4> outputTexture2		SIMUL_RWTEXTURE_REGISTER(1);
uniform RWTexture2D<vec4> outputTexture3		SIMUL_RWTEXTURE_REGISTER(2);

CS_LAYOUT(1,1,1)
shader void CS_PointQueries(uint3 sub_pos: SV_DispatchThreadID )
{
	uint num=0;
	uint byteStride=0;
	//queryInputs.GetDimensions(num,byteStride);
	//if(sub_pos.x>=num)
	//	return;
	VolumeQueryResult res;
	res.pos_km						=queryInputs[sub_pos.x+firstInput];
	res.valid						=1;
	vec3 cloudWorldOffsetKm			=res.pos_km-cornerPosKm;
	vec3 cloudTexCoords				=cloudWorldOffsetKm*inverseScalesKm;
	vec4 lookup						=sample_3d_lod(cloudDensity,cloudSamplerState,cloudTexCoords,0);
	res.density						=lookup.z;
	res.direct_light				=lookup.y;
	res.indirect_light				=lookup.x;
	res.ambient_light				=lookup.w;
	res.precipitation				=GetRainAtOffsetKm( rainMapTexture, cloudWorldOffsetKm, inverseScalesKm, res.pos_km, rainCentreKm, rainRadiusKm);
	
	volumeQueries[sub_pos.x]		=res;
}

CS_LAYOUT(1,1,1)
shader void CS_LineQueries(uint3 sub_pos: SV_DispatchThreadID )
{
	vec3 orig						=queryInputs[sub_pos.x*2+firstInput];
	vec3 start_pos					=orig;
	vec3 end_pos					=queryInputs[sub_pos.x*2+1+firstInput];
	vec3 diff						=end_pos-start_pos;
	float total_dist				=length(diff);
	if(total_dist<=0.0000001)
		return;
	float max_dist					=total_dist;
	LineQueryResult res;
	res.valid						=1;
	res.density						=0.0;
	res.visibility					=1.0;
	res.optical_thickness_km		=0.0;
	res.first_contact_km			=max_dist;
	vec3 dir						=diff/total_dist;
	float start_dist=0;
	// How far under?
	vec3 offsetKm1					=start_pos-cornerPosKm;
	float under						=max(0.0,-offsetKm1.z);
	float over						=max(0.0,offsetKm1.z-1.0/inverseScalesKm.z);
	start_dist						=max(over/max(0.0001,-dir.z),under/max(0.0001,dir.z));
	start_dist						=min(start_dist,total_dist);
	start_pos						+=dir*start_dist;
	
	vec3 offsetKm2					=end_pos-cornerPosKm;
	// How far under?
	float under2					=max(0.0,-offsetKm2.z);
	float over2						=max(0.0,offsetKm2.z-1.0/inverseScalesKm.z);
	float end_dist					=max(over2/max(0.0001,-dir.z),under2/max(0.0001,dir.z));
	end_dist						=min(end_dist,total_dist);
	end_dist						=max(start_dist,end_dist);
	end_pos							=start_pos+dir*end_dist;
	diff							=end_pos-start_pos;
	total_dist						=length(diff);
	
	res.pos1_km						=start_pos;
	res.pos2_km						=end_pos;

	//if(total_dist>0&&dot(dir,res.pos2_km-res.pos1_km)>0.0)
	if(start_dist<max_dist)
	{
		float stepSize						=total_dist/32.0;
		int i=0;
		for( i=0;i<33;i++)
		{
			vec3 pos						=lerp(start_pos,end_pos,float(i)/32.0);
			vec3 cloudTexCoords				=(pos-cornerPosKm)*inverseScalesKm;
			vec4 lookup						=sample_3d_lod(cloudDensity,cloudSamplerState,cloudTexCoords,0);
			if(res.density==0.0&&lookup.z>0)
				res.first_contact_km		=min(res.first_contact_km,length(pos-orig));
			res.density						+=lookup.z/32.0;
			res.visibility					*=1.0-lookup.z;
			res.optical_thickness_km		+=stepSize*lookup.z;
		}
	}
	lineQueryResults[sub_pos.x]	=res;
}

#ifndef DETAIL_NOISE
	#define DETAIL_NOISE 1
#endif

CS_LAYOUT(8,8,8)
shader void CS_Mix( uint3 g : SV_GroupID, uint3 t : SV_GroupThreadID )
{
	uint3 pos=(g*8+t)*amortization+uint3(amortizationOffset);
	vec4 c1=cloudDensity[pos];
	vec4 c2=cloudDensity2[pos];
    vec4 res= lerp(c1,c2,cloud_interp);
	IMAGE_STORE_3D(outputTexture3d,pos,res);
}


CS_LAYOUT(4,4,4)
shader void CS_Mip( uint3 g : SV_GroupID, uint3 t : SV_GroupThreadID )
{
	int3 pos=int3(g*8+t);
	int3 pos2=pos*2;

#ifndef __PSSL__
	vec4 c1= cloudDensity[pos2+int3(0,0,0)];
	vec4 c2= cloudDensity[pos2+int3(1,0,0)];
	vec4 c3= cloudDensity[pos2+int3(0,1,0)];
	vec4 c4= cloudDensity[pos2+int3(1,1,0)];
	vec4 c5= cloudDensity[pos2+int3(0,0,1)];
	vec4 c6= cloudDensity[pos2+int3(1,0,1)];
	vec4 c7= cloudDensity[pos2+int3(0,1,1)];
	vec4 c8= cloudDensity[pos2+int3(1,1,1)];
	vec4 res=0.125*(c1+c2+c3+c4+c5+c6+c7+c8);
	IMAGE_STORE_3D(outputTexture3d,pos,res);
#endif
}


CS_LAYOUT(8,8,8)
shader void CS_Simulate( uint3 g : SV_GroupID, uint3 t : SV_GroupThreadID )
{
	uint3 pos=g*8+t;
	vec4 c1=TEXTURE_LOAD_3D(cloudDensity,pos);
	vec4 c2=TEXTURE_LOAD_3D(cloudDensity2,pos);
	//res.z=1.0;
	//output[pos].rgba=res;
    vec4 res= lerp(c1,c2,cloud_interp);
	IMAGE_STORE_3D(outputTexture3d,pos,res);
	//outputTexture3d[pos].rgba=res;
}
// Given texture position from texCoords, convert to a worldpos with shadowMatrix.
// Then, trace towards sun to find initial intersection with cloud volume
// Then trace down to find first intersection with clouds, if any.
shader vec4 PS_CloudShadow( posTexVertexOutput IN):SV_TARGET
{
	return CloudShadow(cloudDensity,IN.texCoords,shadowMatrix,cornerPosKm,inverseScalesKm);
}

shader vec4 PS_RainMap(posTexVertexOutput IN) : SV_TARGET
{
	float r=MakeRainMap(cloudDensity,IN.texCoords);
	return vec4(r,r,r,r);
}

shader vec4 PS_MoistureAccumulation( posTexVertexOutput IN):SV_TARGET
{
	float m=MoistureAccumulation(cloudShadowTexture,shadowTextureSize,IN.texCoords);
	return vec4(m,m,m,m);
}

struct FarNearDepthPixelOutput
{
	vec4 farColour		SIMUL_RENDERTARGET_OUTPUT(0);
	vec4 nearColour		SIMUL_RENDERTARGET_OUTPUT(1);
	vec4 nearFarDepth	SIMUL_RENDERTARGET_OUTPUT(2);
};

FarNearDepthPixelOutput SimpleRaytrace(posTexVertexOutput IN)
{
	FarNearDepthPixelOutput fn;
	vec2 texCoords	=mixedResTransformXYWH.xy + IN.texCoords.xy*mixedResTransformXYWH.zw;
	vec4 clip_pos			=vec4(-1.0,1.0,1.0,1.0);
	clip_pos.x				+=2.0*texCoords.x;
	clip_pos.y				-=2.0*texCoords.y;
	float sineFactor		=1.0/length(clip_pos.xyz);
	vec3 view				=-normalize(mul(invViewProj,clip_pos).xyz);
	vec4 dlookup			=texture_cube_lod(depthTexture,view,0);
	RaytracePixelOutput p	=RaytraceCloudsForward(
									cloudDensity
									,rainMapTexture
									,noiseTexture3D
									,lightTableTexture
									,illuminationTexture
									,rainbowLookupTexture
									,coronaLookupTexture
									,lossTexture
									,inscTexture
									,skylTexture,volumeInscatterTexture
									,true
									,dlookup
									,view
									,clip_pos
									,IN.texCoords.xy
									,false
									,true
									,cloudIrRadiance1,cloudIrRadiance2
									);
	//if(p.colour.a>=1.0)
	//   discard;
	p.colour.rgb*=exposure;
	fn.farColour=p.colour;
	fn.nearColour=p.nearColour;
	fn.nearFarDepth = p.nearFarDepth;
	return fn;
}

shader FarNearDepthPixelOutput  PS_SimpleRaytrace(posTexVertexOutput IN) 
{
	FarNearDepthPixelOutput f= SimpleRaytrace(IN);
	return f;
}

shader FarNearPixelOutput PS_Lightpass(posTexVertexOutput IN) 
{
	vec2 texCoords			=mixedResTransformXYWH.xy+IN.texCoords.xy*mixedResTransformXYWH.zw;
	texCoords				=frac(texCoords);
	vec4 clip_pos			=vec4(-1.0,1.0,1.0,1.0);
	clip_pos.x				+=2.0*texCoords.x;
	clip_pos.y				-=2.0*texCoords.y;
	vec3 view				=-normalize(mul(invViewProj,clip_pos).xyz);
	vec4 dlookup			=texture_cube_lod(depthTexture,view,0);
	FarNearPixelOutput f	=Lightpass(	cloudDensity
									,noiseTexture3D
									,dlookup
									,view
									,clip_pos
									,sourcePosKm
									,sourceRadiusKm
									,spectralFluxOver1e6
									,minCosine
									,maxRadiusKm
									,irradianceThreshold
									);
	return f;
}

void Raytrace(int2 pos,bool rain)
{
	if(updateTexture[pos].r==0.0)
		return;
	vec2 texCoords			=vec2(pos.xy)/vec2(targetTextureSize);
	vec4 clip_pos			=vec4(-1.0,1.0,1.0,1.0);
	clip_pos.x				+=2.0*texCoords.x;
	clip_pos.y				-=2.0*texCoords.y;
	float sineFactor		=1.0/length(clip_pos.xyz);
	vec3 view				=-normalize(mul(invViewProj,clip_pos).xyz);
	vec4 dlookup			=texture_cube_lod(depthTexture,view,0);
	// Transform view into the proper frustum
	vec4 frustum_clip_pos	=mul(viewProj,vec4(view,1.0));
	frustum_clip_pos		/=frustum_clip_pos.w;
//	if(frustum_clip_pos.z<0.0||frustum_clip_pos.x<-1.0||frustum_clip_pos.x>1.0||frustum_clip_pos.y<-1.0||frustum_clip_pos.y>1.0)
//		return;
	vec2 atmosTexCoords		=0.5*(frustum_clip_pos.xy+vec2(1.0,1.0));
	atmosTexCoords.y		=1.0-atmosTexCoords.y;
	atmosTexCoords			=frac((-mixedResTransformXYWH.xy+atmosTexCoords)*mixedResTransformXYWH.zw);

	RaytracePixelOutput f	=RaytraceCloudsForward(
									cloudDensity
									,rainMapTexture
									,noiseTexture3D
									,lightTableTexture
									,illuminationTexture
									,rainbowLookupTexture
									,coronaLookupTexture
									,lossTexture
									,inscTexture
									,skylTexture,volumeInscatterTexture
									,true
									,dlookup
									,view
									,clip_pos
									,atmosTexCoords
									,true
									,rain
									,cloudIrRadiance1,cloudIrRadiance2
									);
	vec4 t2d				=texture_cube_lod(clouds2DTexture,view,0);
	// blend:
	vec4 c					=t2d;
	c.rgb					*=f.colour.a;
	c.rgb					+=f.colour.rgb;
	c.a						=t2d.a*f.colour.a;
	if(frustum_clip_pos.x>=-1.0&&frustum_clip_pos.x<=1.0&&frustum_clip_pos.y>=-1.0&&frustum_clip_pos.y<=1.0)
	{
//		c.rg=frac(atmosTexCoords.xy);
	}
	outputTexture1[pos]		=c;
	float n					=float(dlookup.y==0);
	vec4 cn					=t2d;
	cn.rgb					*=f.nearColour.a*n;
	cn.a					=1.0-n*(1.0-t2d.a);
	cn.rgb					+=f.nearColour.rgb;
	cn.a					*=f.nearColour.a;
	outputTexture2[pos]		=cn;
	outputTexture3[pos]		=f.nearFarDepth;
}

void CS_Raytrace(uint3 g : SV_GroupID, uint3 t : SV_GroupThreadID,bool rain)
{
	int2 idx				=int2(g.xy*8+t.xy);
	int2 pos				=targetRange.xy+idx*amortization+amortizationOffset.xy;
	if(pos.x>=edge.x||pos.y>=edge.y)
		return;
	if(pos.x>=targetRange.z||pos.y>=targetRange.w)
		return;
	Raytrace(pos,rain);
}

CS_LAYOUT(8,8,1)
shader void CS_Edge(uint3 g : SV_GroupID, uint3 t : SV_GroupThreadID)
{
	bool rain=true;
	int2 idx				=int2(g.xy*8+t.xy);
	int2 pos				=targetRange.xy+idx;
	if(pos.x>=targetRange.z||pos.y>=targetRange.w)
		return;
	vec4 u=updateTexture[pos];
	// current value is b, old value is r
	if(u.r<1.0&&u.b>0.0)
	{
		Raytrace(pos,  rain);
	//	outputTexture1[pos]		=vec4(1,0,0,0);
	}
}

FarNearDepthPixelOutput PS_Raytrace(posTexVertexOutput IN,bool rain)
{
	// We transform from the target texCoords (0-1,0-1) passed from the vertex shader,
	//  to the texCoords of our "window". These wrap around 
	vec2 texCoords			=mixedResTransformXYWH.xy+IN.texCoords.xy*mixedResTransformXYWH.zw;
	vec4 clip_pos			=vec4(-1.0,1.0,1.0,1.0);
	clip_pos.x				+=2.0*texCoords.x;
	clip_pos.y				-=2.0*texCoords.y;
	float sineFactor		=1.0/length(clip_pos.xyz);
	vec3 view				=-normalize(mul(invViewProj,clip_pos).xyz);
	vec4 dlookup 			=texture_cube_lod(depthTexture,view,0);

	RaytracePixelOutput f	=RaytraceCloudsForward(
									cloudDensity
									,rainMapTexture
									,noiseTexture3D
									,lightTableTexture
									,illuminationTexture
									,rainbowLookupTexture
									,coronaLookupTexture
									,lossTexture
									,inscTexture
									,skylTexture,volumeInscatterTexture
									,true
									,dlookup
									,view
									,clip_pos
									,IN.texCoords.xy
									,true
									,rain
									,cloudIrRadiance1,cloudIrRadiance2
									);
	FarNearDepthPixelOutput fn;
	fn.farColour=f.colour;
	fn.nearColour=f.nearColour;
	fn.nearFarDepth = f.nearFarDepth;
	return fn;
}

shader FarNearDepthPixelOutput PS_Raytrace(posTexVertexOutput IN)
{
	return PS_Raytrace(IN,true);
}

shader FarNearDepthPixelOutput PS_RaytraceNoRain(posTexVertexOutput IN)
{
	return PS_Raytrace(IN,false);
}

CS_LAYOUT(8,8,1)
shader void CS_RaytraceRain(uint3 g : SV_GroupID, uint3 t : SV_GroupThreadID)
{
	CS_Raytrace(g,t,true);
}


CS_LAYOUT(8,8,1)
shader void CS_RaytraceNoRain(uint3 g : SV_GroupID, uint3 t : SV_GroupThreadID)
{
	CS_Raytrace(g,t,false);
}


shader FarNearDepthPixelOutput PS_RaytraceStatic(posTexVertexOutput IN)
{
	vec2 texCoords			=mixedResTransformXYWH.xy+IN.texCoords.xy*mixedResTransformXYWH.zw;
	vec4 clip_pos			=vec4(-1.0,1.0,1.0,1.0);
	clip_pos.x				+=2.0*texCoords.x;
	clip_pos.y				-=2.0*texCoords.y;
	float sineFactor		=1.0/length(clip_pos.xyz);
	vec3 view				=-normalize(mul(invViewProj,clip_pos).xyz);
	vec4 dlookup 			=texture_cube_lod(depthTexture,view,0);
	RaytracePixelOutput f	=RaytraceCloudsStatic(
									cloudDensity
									,rainMapTexture
									,noiseTexture3D
									,lightTableTexture
									,illuminationTexture
									,rainbowLookupTexture
									,coronaLookupTexture
									,lossTexture
									,inscTexture
									,skylTexture,volumeInscatterTexture
									,true
									,dlookup
									,texCoords
									,true
									,false
									,cloudIrRadiance1
									,cloudIrRadiance2
									);
	FarNearDepthPixelOutput fn;
	fn.farColour	=f.colour;
	fn.nearColour	=f.nearColour;

	fn.nearFarDepth	=f.nearFarDepth;

	return fn;

}

shader posTexVertexOutput VS_CrossSection(idOnly IN)
{
	return VS_ScreenQuad(IN,rect);
}

shader posTexVertexOutput VS_PlacementVolume(idOnly IN)
{
	posTexVertexOutput OUT;
	vec2 poss[4];
	poss[0]			=vec2(0.0, 0.0);
	poss[1]			=vec2(1.0, 0.0);
	poss[2]			=vec2(1.0, 1.0);
	poss[3]			=vec2(0.0, 1.0);
	vec3 pos		=vec3(0,0,0);
	int odd			=int(IN.vertex_id%uint(2));
	if(IN.vertex_id<8)
	{
		pos.z=0.0;
		int j=(int(IN.vertex_id)/2+odd);
		pos.xy=poss[j%4];
	}
	else if(IN.vertex_id<16)
	{
		pos.z=1.0;
		int i=int(IN.vertex_id)-8;
		int j=((i)/2+odd);
		pos.xy=poss[j%4];
	}
	else
	{
		int i=int(IN.vertex_id)-16;
		int j=i/2;
		pos.z=float(i%2);
		pos.xy=poss[j%4];
	}

	OUT.hPosition	=mul(invViewProj,vec4(pos,1.0));
	OUT.texCoords	=vec2(pos.z,0);
	return OUT;
}

shader vec4 PS_PlacementVolume(posTexVertexOutput IN):SV_TARGET
{
	return vec4(1.0,1.0,IN.texCoords.x,1.0);
}

shader vec4 PS_Simple(posTexVertexOutput IN):SV_TARGET
{
    return texture_wrap(cloudShadowTexture,IN.texCoords.xy);
}

shader vec4 PS_ShowNoise(posTexVertexOutput IN):SV_TARGET
{
    vec4 lookup=texture_wrap(cloudShadowTexture,IN.texCoords.xy);
	return vec4(0.5*(lookup.rgb+1.0),1.0);
}

shader vec4 PS_Show3DNoise(posTexVertexOutput IN):SV_TARGET
{
	float z			=floor(8.0f*IN.texCoords.x)/8.0f;
	//if(IN.texCoords.y<0.5)
		z=0.5;
	vec4 lookup		=texture_3d_nearest_lod(noiseTexture3D,vec3(IN.texCoords,z),0);
	//lookup=abs(lookup);
	return vec4(0.5*(lookup.rgb+1.0),1.0);
}

shader vec4 PS_ShowShadow(posTexVertexOutput IN):SV_TARGET
{
    vec4 lookup			=texture_clamp_lod(cloudShadowTexture,IN.texCoords.xy,0);
	return vec4(lookup.rgb*lookup.a,1.0);
}

#define CROSS_SECTION_STEPS 32
vec4 CrossSection(vec2 texCoords,float yz)
{
	vec3 texc	=crossSectionOffset+vec3(texCoords.x,yz*texCoords.y,(1.0-yz)*texCoords.y);
	int i		=0;
	vec3 accum	=vec3(0.0,0.5,1.0);
	texc.y		+=0.5*(1.0-yz)/float(CROSS_SECTION_STEPS);
	//texc.z+=0.5*yz/(float)CROSS_SECTION_STEPS;
	vec2 uv = vec2(0,0);
	for(i=0;i<CROSS_SECTION_STEPS;i++)
	{
		vec4 density=texture_wwc_lod(cloudDensity,texc,1);
		vec3 colour=vec3(.5,.5,.5)*(lightResponse.x*density.y+lightResponse.y*density.x);
		colour.gb+=vec2(.125,.25)*(lightResponse.z*density.w);
		float opacity=density.z;
		colour*=opacity;
		accum*=1.0-opacity;
		accum += colour; 
	//	accum *=1-0.1*(1.0-density.y);
		uv += density.xy;
		texc.y-=(1.0-yz)/float(CROSS_SECTION_STEPS);
		texc.z+=yz/float(CROSS_SECTION_STEPS);
	}
//	accum.rg = uv / float(CROSS_SECTION_STEPS);
    return vec4(accum,1);
}

shader vec4 PS_CrossSection(posTexVertexOutput IN):SV_TARGET
{
    return CrossSection(IN.texCoords,yz);
}

BlendState Blend1
{
	BlendEnable[0]	=FALSE;
	BlendEnable[1] = FALSE;
	BlendEnable[2] = FALSE;
	SrcBlend		=ONE;
	DestBlend		=SRC_ALPHA;
    BlendOp			=ADD;
    SrcBlendAlpha	=ZERO;
    DestBlendAlpha	=SRC_ALPHA;
    BlendOpAlpha	=ADD;
};

BlendState AddBlend2
{
	BlendEnable[0]	=TRUE;
	BlendEnable[1] = TRUE;
	BlendEnable[2] = FALSE;
	SrcBlend		=ONE;
	DestBlend		=ONE;
    BlendOp			=ADD;
    SrcBlendAlpha	=ZERO;
    DestBlendAlpha	=ONE;
    BlendOpAlpha	=ADD;
};

VertexShader vs_fullscreen_5_0 = CompileShader(vs_5_0, VS_SimpleFullscreen());

fxgroup raytrace
{
	technique simple
	{
		pass p0
		{
			SetComputeShader(CompileShader(cs_5_0,CS_RaytraceNoRain()));
		}
		}
	technique lightpass
	{
		pass p0
		{
			SetBlendState(AddBlend2,vec4( 0.0, 0.0, 0.0, 0.0 ), 0xFFFFFFFF );
			SetDepthStencilState(DisableDepth,0);
			SetRasterizerState( RenderNoCull );
			SetVertexShader(vs_fullscreen_5_0);
			SetPixelShader(CompileShader(ps_5_0,PS_Lightpass()));
		}
		}
	technique edge
	{
		pass p0 
		{
			SetComputeShader(CompileShader(cs_5_0,CS_Edge()));
		}
		}
	technique full_compute
	{
		pass p0 
		{
			SetComputeShader(CompileShader(cs_5_0,CS_RaytraceRain()));
		}
		}
	technique no_rain_compute
	{
		pass p0 
		{
			SetComputeShader(CompileShader(cs_5_0,CS_RaytraceNoRain()));
		}
		}
	technique stat
	{
		pass p0 
		{
			SetBlendState(Blend1,vec4( 0.0, 0.0, 0.0, 0.0 ), 0xFFFFFFFF );
			SetDepthStencilState(WriteDepth,0);
			SetRasterizerState( RenderNoCull );
			SetVertexShader(vs_fullscreen_5_0);
			SetPixelShader(CompileShader(ps_5_0,PS_RaytraceStatic()));
		}
		}
	}

technique cloud_shadow
{
    pass p0 
    {
		SetDepthStencilState(DisableDepth,0);
        SetRasterizerState( RenderNoCull );
		SetBlendState(NoBlend,vec4( 0.0, 0.0, 0.0, 0.0 ), 0xFFFFFFFF );
		SetVertexShader(vs_fullscreen_5_0);
        SetGeometryShader(NULL);
		SetPixelShader(CompileShader(ps_5_0,PS_CloudShadow()));
    }
}


technique rain_map
{
    pass p0 
    {
		SetDepthStencilState(DisableDepth,0);
        SetRasterizerState( RenderNoCull );
		SetBlendState(NoBlend,vec4( 0.0, 0.0, 0.0, 0.0 ), 0xFFFFFFFF );
		SetVertexShader(vs_fullscreen_5_0);
        SetGeometryShader(NULL);
		SetPixelShader(CompileShader(ps_5_0,PS_RainMap()));
    }
}

technique mix_clouds
{
    pass p0
    {
		SetComputeShader(CompileShader(cs_5_0,CS_Mix()));
    }
}

technique cloud_mip
{
    pass p0
    {
		SetComputeShader(CompileShader(cs_5_0,CS_Mip()));
    }
}

technique simulate_clouds
{
    pass p0
    {
		SetComputeShader(CompileShader(cs_5_0,CS_Simulate()));
    }
}

technique moisture_accumulation
{
    pass p0
    {
		SetRasterizerState( RenderNoCull );
		SetDepthStencilState( DisableDepth, 0 );
		SetBlendState(DontBlend, vec4( 0.0, 0.0, 0.0, 0.0 ), 0xFFFFFFFF );
		SetVertexShader(vs_fullscreen_5_0);
        SetGeometryShader(NULL);
		SetPixelShader(CompileShader(ps_5_0,PS_MoistureAccumulation()));
    }
}

technique cloud_placement_volume
{
    pass p0 
    {
		SetDepthStencilState(DisableDepth,0);
        SetRasterizerState( wireframeRasterizer );
		SetBlendState(NoBlend,vec4( 0.0, 0.0, 0.0, 0.0 ), 0xFFFFFFFF );
		SetVertexShader(CompileShader(vs_4_0,VS_PlacementVolume()));
        SetGeometryShader(NULL);
		SetPixelShader(CompileShader(ps_4_0,PS_PlacementVolume()));
    }
}

technique cross_section
{
    pass p0 
    {
		SetDepthStencilState(DisableDepth,0);
        SetRasterizerState( RenderNoCull );
		SetBlendState(NoBlend,vec4( 0.0, 0.0, 0.0, 0.0 ), 0xFFFFFFFF );
		SetVertexShader(CompileShader(vs_4_0,VS_CrossSection()));
        SetGeometryShader(NULL);
		SetPixelShader(CompileShader(ps_4_0,PS_CrossSection()));
    }
}

technique simple
{
    pass p0
    {
		SetRasterizerState( RenderNoCull );
		SetDepthStencilState( DisableDepth, 0 );
		SetBlendState(DontBlend, vec4( 0.0, 0.0, 0.0, 0.0 ), 0xFFFFFFFF );
		SetVertexShader(CompileShader(vs_4_0,VS_CrossSection()));
        SetGeometryShader(NULL);
		SetPixelShader(CompileShader(ps_4_0,PS_Simple()));
    }
}

technique show_noise
{
    pass p0
    {
		SetRasterizerState( RenderNoCull );
		SetDepthStencilState( DisableDepth, 0 );
		SetBlendState(DontBlend, vec4( 0.0, 0.0, 0.0, 0.0 ), 0xFFFFFFFF );
		SetVertexShader(CompileShader(vs_4_0,VS_CrossSection()));
        SetGeometryShader(NULL);
		SetPixelShader(CompileShader(ps_4_0,PS_ShowNoise()));
    }
}

technique show_3d_noise
{
    pass p0
    {
		SetRasterizerState( RenderNoCull );
		SetDepthStencilState( DisableDepth, 0 );
		SetBlendState(DontBlend, vec4( 0.0, 0.0, 0.0, 0.0 ), 0xFFFFFFFF );
		SetVertexShader(CompileShader(vs_4_0,VS_CrossSection()));
        SetGeometryShader(NULL);
		SetPixelShader(CompileShader(ps_4_0,PS_Show3DNoise()));
    }
}

technique show_shadow
{
    pass p0
    {
		SetRasterizerState( RenderNoCull );
		SetDepthStencilState( DisableDepth, 0 );
		SetBlendState(DontBlend, vec4( 0.0, 0.0, 0.0, 0.0 ), 0xFFFFFFFF );
		SetVertexShader(CompileShader(vs_4_0,VS_CrossSection()));
        SetGeometryShader(NULL);
		SetPixelShader(CompileShader(ps_4_0,PS_ShowShadow()));
    }
}

technique point_queries
{
    pass p0
    {
		SetComputeShader(CompileShader(cs_5_0,CS_PointQueries()));
    }
}
technique line_queries
{
    pass p0
    {
		SetComputeShader(CompileShader(cs_5_0,CS_LineQueries()));
    }
}